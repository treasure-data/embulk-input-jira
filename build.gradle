plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "maven"
    id 'maven-publish'
    id "signing"
}

repositories {
    mavenCentral()
}

version = "0.3.1-SNAPSHOT"
group = "com.treasuredata.embulk.plugins"
description = "JIRA Embulk input plugin."

sourceCompatibility = 1.8
targetCompatibility = 1.8

def embulkVersion = '0.10.32'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"

    compile('org.embulk:embulk-util-config:0.3.1') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }
    compile('org.embulk:embulk-util-retryhelper-jetty92:0.8.2')
    compile 'org.embulk:embulk-util-timestamp:0.2.1'
    compile('org.embulk:embulk-util-guess:0.1.2')

    compile('org.embulk:embulk-util-json:0.1.1') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'org.msgpack', module: 'msgpack-core'
    }

    // Explicit dependencies for embulk-util-* that matches with Embulk 0.10.32
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7'
    compile 'javax.validation:validation-api:1.1.0.Final'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'

    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.27'
    // Java EE dependencies (required on Java 9+) for jersey-client
    compile "javax.xml.bind:jaxb-api:2.2.11"
    compile "com.sun.xml.bind:jaxb-core:2.2.11"
    compile "com.sun.xml.bind:jaxb-impl:2.2.11"
    compile "javax.activation:activation:1.1.1"

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.4'

    testCompile "junit:junit:4.13"
    testCompile("org.embulk:embulk-core:$embulkVersion") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }
    testCompile "junit:junit:4.10"
    testCompile "org.embulk:embulk-junit4:$embulkVersion"
    testCompile "org.embulk:embulk-core:$embulkVersion"
    testCompile "org.embulk:embulk-core:$embulkVersion:tests"
    testCompile "org.embulk:embulk-deps:$embulkVersion"
    testCompile "org.mockito:mockito-core:2.28.2"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.input.jira.JiraInputPlugin"
    category = "input"
    type = "jira"
}

javadoc {
    options {
        locale = "en_US"
        encoding = "UTF-8"
    }
}

jar {
    from rootProject.file("LICENSE")
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://github.com/treasure-data/embulk-input-jira"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    // Authors after reimplementing it in Java.
                    developer {
                        name = "Phu Nguyen"
                        email = "phu@treasure-data.com"
                    }
                    developer {
                        name = "Huy Le"
                        email = "huy.lenq@gmail.com"
                    }
                    developer {
                        name = "Thanh Le"
                        email = "legiangthanh@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/treasure-data/embulk-input-jira.git"
                    developerConnection = "scm:git:git@github.com:treasure-data/embulk-input-jira.git"
                    url = "https://github.com/treasure-data/embulk-input-jira"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

gem {
    authors = ['uu59', 'yoshihara']
    email = ['k@uu59.org', 'h.yoshihara@everyleaf.com']
    summary = project.description
    homepage = 'https://github.com/treasure-data/embulk-input-jira'
    licenses = ['Apache-2.0']
}

gemPush {
    host = "https://rubygems.org"
}
